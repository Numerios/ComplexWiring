// Credits to CrazyPants (EnderIO) for this gradle buildscript! :)
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge' // adds the forge dependency

version = "0.0.0.0_alpha"
group = "num.complexwiring"
archivesBaseName = "complexwiring" // the name that all artifacts will use as a base. artifacts names follow this pattern: [baseName]-[appendix]-[version]-[classifier].[extension]

minecraft {
    version = "1.7.2-10.12.2.1121"

    assetDir = "debug/assets"
    // the place for ForgeGradle to download the assets. The assets that the launcher gets and stuff

    // replacing stuff in the source
    replace '@VERSION@', project.version
    replace '@MC_VERSION@', version
}

dependencies {
    compile "codechicken:CodeChickenLib:1.7.2-1.1.1.81:dev"
    compile "codechicken:ForgeMultipart:1.7.2-1.1.0.282:dev"
    compile files("lib/buildcraft-6.0.10-dev.jar")
}

// configure the source folders
sourceSets {
    main {
        java {
            srcDir 'common'
            // exclude 'some exclusion'
            // include 'some inclusion'
        }
        resources {
            srcDir 'resources'
            // exclude 'some exclusion'
            // include 'some inclusion'
        }
    }
}

processResources {
    // excludes psd files
    exclude '**/*.psd'

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        // ${version} and ${mcversion} are the exact strings being replaced
        // expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, that's not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// --------------------
// extra jar section
// -------------------

// add a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    exclude '**/*.psd'
    classifier = 'sources'
}

// because the normal output has been made to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

task apiJar(type: Jar) {
    from sourceSets.main.allSource
    include 'num/complexwiring/api/**'
    classifier = 'api'
}

// make sure all of these happen when we run build
build.dependsOn sourceJar, deobfJar, apiJar