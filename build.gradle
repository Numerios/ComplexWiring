// Credits to CrazyPants (EnderIO) for this gradle buildscript! :)
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge' // adds the forge dependency

ext.configFile = file "build.properties"  // loads the properties file as configFile

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
    // enables access to the fields in properties as config.[property]
}
def isDev = project.hasProperty('isDev')
def dev_check = isDev ? ("&" + "devbuild=1") : ""

String web_version = ('http://complexwiring.numerios.eu/ci-stuff.php?version=' + config.mod_version + dev_check).toURL().getText();

version = web_version ?: config.mod_version
group = "num.complexwiring"
archivesBaseName = "ComplexWiring" // the name that all artifacts will use as a base. artifacts names follow this pattern: [baseName]-[appendix]-[version]-[classifier].[extension]

minecraft {
    version = config.minecraft_version + "-" + config.forge_version

    // the place for ForgeGradle to download the assets. The assets that the launcher gets and stuff
    assetDir = "debug/assets"

    // replacing stuff in the source
    replace '@VERSION@', project.version
}

dependencies {
    //  compile "codechicken:CodeChickenLib:1.7.2-1.1.1.81:dev"
    //  compile "codechicken:ForgeMultipart:1.7.2-1.1.0.282:dev"
    //  compile files("lib/buildcraft-6.0.10-dev.jar")
}

// configure the source folders
sourceSets {
    main {
        java {
            srcDir 'common'
            // exclude 'some exclusion'
            // include 'some inclusion'
        }
        resources {
            srcDir 'resources'
            // exclude 'some exclusion'
            // include 'some inclusion'
        }
    }
}

processResources {
    // excludes psd files
    exclude '**/*.psd'

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        // ${version} and ${mcversion} are the exact strings being replaced
        // expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, that's not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// --------------------
// extra jar section
// -------------------

// add a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    exclude '**/*.psd'
    classifier = 'sources'
}

// because the normal output has been made to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

task apiJar(type: Jar) {
    from sourceSets.main.allSource
    include 'num/complexwiring/api/**'
    classifier = 'api'
}

// make sure all of these happen when we run build
build.dependsOn sourceJar, deobfJar, apiJar